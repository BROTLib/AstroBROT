<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_RADEC2HADEC" Id="{0862c677-7b7c-46dd-9ea7-b8a7ab9f8ddc}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RADEC2HADEC
VAR_INPUT
	jd				: LREAL;		// Julian date
	ra				: LREAL;		// Right Ascension (ICRS) in degrees
	dec				: LREAL;		// Declination (ICRS) in degrees
	lon				: LREAL;		// Longitude in degrees
END_VAR
VAR_OUTPUT
	ha_out			: LREAL;		// Hour angle (APPARENT) in degrees
	dec_out			: LREAL;		// Declination (APPARENT) in degrees
END_VAR
VAR
	lmst			: LREAL;
	last			: LREAL;
	d_psi			: LREAL;
	d_eps			: LREAL;
	eps				: LREAL;
	J_now			: LREAL;
	fbprecess		: FB_PRECESS;
	co_nutate		: FB_CO_NUTATE;
	co_aberration	: FB_CO_ABERRATION;
	d_ra_abr		: LREAL;		// Difference in RA due to aberration
	d_dec_abr		: LREAL;		// Difference in Dec due to aberration
	d_ra_nut		: LREAL;		// Difference in RA due to nutation
	d_dec_nut		: LREAL;		// Difference in Dec due to nutation
	ra_pre, dec_pre	: LREAL;		// precessed coords
END_VAR
VAR CONSTANT
	d2r				: LREAL := PI/180.0;
	jd2000			: LREAL := 2451545.0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// This code calculates the Hour Angle and Declination (APPARENT) from the Right Ascension and Declination (ICRS)
// This includes P03 precession and IAU2000B nutation as well as aberration (IDL libary)
// adapted from: https://github.com/wlandsman/IDLAstro/blob/master/pro/eq2hor.pro (IDL libary)
// this version is written by Lukas Melzig
(* ******************************************************************************
 PRECESS coordinates to current date
 (uses astro lib procedure PRECESS.pro)
*)
J_now := (JD - jd2000)/365.25 + 2000.0; // compute current equinox
fbprecess( 
		ra := ra, 
		dec := dec, 
		equinox1 := 2000.0, 
		equinox2 := J_now, 
		ra_precessed => ra_pre, 
		dec_precessed => dec_pre);

(******************************************************************************
 calculate NUTATION and ABERRATION Corrections to Ra-Dec 
*)

co_nutate(	
		jd := jd, 
		ra := ra_pre, 
		dec := dec_pre, 
		d_ra => d_ra_nut, 
		d_dec => d_dec_nut,
		eps=>eps, 
		d_psi=>d_psi);

co_aberration(	
		jd := jd, 
		ra := ra_pre, 
		dec := dec_pre, 
		d_ra => d_ra_abr, 
		d_dec => d_dec_abr,
		eps := eps);
ra := ra_pre + d_ra_abr + d_ra_nut;
dec := dec_pre + d_dec_abr + d_dec_nut;
(***************************************************************************************
Calculate LOCAL MEAN SIDEREAL TIME *)
lmst := jd2lst(JD:=jd, lon:=lon);
// calculate local APPARENT sidereal time
last := lmst + d_psi *COS(eps)/3600.0 ; //add correction in degrees

(******************************************************************************
 Find hour angle (in DEGREES) *)
ha_out := last - ra;
ha_out := MODABS(ha_out, 360.0);
// apparent declination
dec_out := dec;
]]></ST>
    </Implementation>
    <LineIds Name="FB_RADEC2HADEC">
      <LineId Id="172" Count="1" />
      <LineId Id="318" Count="0" />
      <LineId Id="317" Count="0" />
      <LineId Id="174" Count="11" />
      <LineId Id="193" Count="12" />
      <LineId Id="214" Count="22" />
    </LineIds>
  </POU>
</TcPlcObject>