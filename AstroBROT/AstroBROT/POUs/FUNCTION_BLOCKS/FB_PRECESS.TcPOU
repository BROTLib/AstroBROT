<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_PRECESS" Id="{879c3673-6766-4546-9283-3aa893f2e05f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PRECESS
VAR_INPUT
	eq_coord_in: S_SkyCoordEquatorial;
	equinox1,
	equinox2: LREAL;
END_VAR
VAR_OUTPUT
	eq_coord_precessed:	S_SkyCoordEquatorial;
END_VAR
VAR
	ra_rad, dec_rad: LREAL;
	a,b,c,t: LREAL;
	x, x2: ARRAY[0..2] OF LREAL;
	st1: LREAL;
	rot: ARRAY[0..2,0..2] OF LREAL;
	sina, sinb, sinc, cosa,cosb,cosc: LREAL;
END_VAR
VAR CONSTANT
	d2r : LREAL := PI/180.0;
	sec_to_rad : LREAL := d2r/3600.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// https://idlastro.gsfc.nasa.gov/ftp/pro/astro/precess.pro

(*
; RESTRICTIONS:
;       Accuracy of precession decreases for declination values near 90 
;       degrees.  PRECESS should not be used more than 2.5 centuries from
;       2000 on the FK5 system (1950.0 on the FK4 system).
; PROCEDURE:
;       Algorithm from Computational Spherical Astronomy by Taff (1983), 
;       p. 24. (FK4). FK5 constants from "Astronomical Almanac Explanatory
;       Supplement 1992, page 104 Table 3.211.1.
*)

// deg 2 radian
ra_rad := eq_coord_in.ra*d2r;
dec_rad := eq_coord_in.dec*d2r;


a := COS( dec_rad );
x[0] := a*COS(ra_rad); x[1] := a*SIN(ra_rad); x[2] := SIN(dec_rad);

//*** PREMAT code starts here
ST1 := 0.001*( equinox1 - 2000.0);
t := 0.001*( equinox2 - equinox1);
//  Compute 3 rotation angles
A := sec_to_rad * T * (23062.181 + ST1*(139.656 +0.0139*ST1) + T*(30.188 - 0.344*ST1+17.998*T));
B := sec_to_rad * T * T * (79.280 + 0.410*ST1 + 0.205*T) + A;
C := sec_to_rad * T * (20043.109 - ST1*(85.33 + 0.217*ST1) + T*(-42.665 - 0.217*ST1 -41.833*T));
  sina := SIN(a); sinb := SIN(b);  sinc := SIN(c);
  cosa := COS(a); cosb := COS(b);  cosc := COS(c);

rot[0,0] :=  cosa*cosb*cosc-sina*sinb; 	rot[0,1] := sina*cosb+cosa*sinb*cosc; 	rot[0,2] := cosa*sinc;
rot[1,0] := -cosa*sinb-sina*cosb*cosc; 	rot[1,1] := cosa*cosb-sina*sinb*cosc; 	rot[1,2] := -sina*sinc;
rot[2,0] := -cosb*sinc;					rot[2,1] := -sinb*sinc; 				rot[2,2] := cosc;

//*** PREMAT code ends here

//x2 := rot* x; //rotate to get output direction cosines
x2[0] := rot[0,0]*x[0] + rot[1,0]*x[1] + rot[2,0]*x[2];
x2[1] := rot[0,1]*x[0] + rot[1,1]*x[1] + rot[2,1]*x[2];
x2[2] := rot[0,2]*x[0] + rot[1,2]*x[1] + rot[2,2]*x[2];

ra_rad := atan2(x2[1],x2[0]);
dec_rad := ASIN(x2[2]);

eq_coord_precessed.ra := ra_rad/d2r;
eq_coord_precessed.ra := MODABS(eq_coord_precessed.ra, 360.0);            //RA between 0 AND 360 degrees
eq_coord_precessed.dec := dec_rad/d2r;]]></ST>
    </Implementation>
    <LineIds Name="FB_PRECESS">
      <LineId Id="79" Count="0" />
      <LineId Id="104" Count="1" />
      <LineId Id="134" Count="0" />
      <LineId Id="106" Count="1" />
      <LineId Id="103" Count="0" />
      <LineId Id="131" Count="2" />
      <LineId Id="130" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="80" Count="1" />
      <LineId Id="72" Count="1" />
      <LineId Id="78" Count="0" />
      <LineId Id="11" Count="28" />
      <LineId Id="41" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>