<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_PRECESS" Id="{879c3673-6766-4546-9283-3aa893f2e05f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_PRECESS
VAR_INPUT
	ra				: LREAL;		// RA in degrees
	dec				: LREAL;		// Dec in degrees
	equinox1		: LREAL;
	equinox2		: LREAL;
END_VAR
VAR_OUTPUT
	ra_precessed	: LREAL;
	dec_precessed	: LREAL;
END_VAR
VAR
	ra_rad			: LREAL;		// RA in rad
	dec_rad			: LREAL;		// Dec in rad
	t				: LREAL;		// the elapsed time in Julian centuries since J2000 TT (days)
	EPS0, PSIA, OMEGAA, CHIA : LREAL;
	x, x2			: ARRAY[0..2] OF LREAL;
	rot				: ARRAY[0..2,0..2] OF LREAL;
	SA, CA, SB,CB,SC,CC,SD,CD: LREAL;
END_VAR
VAR CONSTANT
	d2r 			: LREAL := PI/180.0;
	sec_to_rad 		: LREAL := d2r/3600.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
based on: NOVAS_F3.1.f -> SUBROUTINE PRECES
Kaplan, G., Bartlett, J., Monet, A., Bangert, J., & Puatua, W. (2011) User's Guide to NOVAS Version F3.1 (Washington, DC: USNO).

 THIS SUBROUTINE PRECESSES EQUATORIAL RECTANGULAR COORDINATES FROM
 ONE EPOCH TO ANOTHER.  THE COORDINATES ARE REFERRED TO THE MEAN
 DYNAMICAL EQUATOR AND EQUINOX OF THE TWO RESPECTIVE EPOCHS.  SEE
 EXPLANATORY SUPPLEMENT TO THE ASTRONOMICAL ALMANAC, PP. 103-104,
 AND CAPITAINE ET AL. (2003), ASTRONOMY AND ASTROPHYSICS 412,
 567-586.
*)

// deg 2 radian
ra_rad := ra*d2r;
dec_rad := dec*d2r;

x[0] := COS(dec_rad )*COS(ra_rad);
x[1] := COS(dec_rad )*SIN(ra_rad);
x[2] := SIN(dec_rad);


// t is  is the elapsed time in Julian centuries since J2000 TT (days),
t := 0.001*(equinox2 - equinox1); 
IF equinox2 = 2000.0 THEN
	t := -1.0*t;
END_IF
EPS0 := sec_to_rad*	84381.406;
PSIA := sec_to_rad*	( ( ( ( -    0.0000000951   * t
							+    0.000132851  ) * t
							-    0.00114045   ) * t
							-    1.0790069    ) * t
							+ 5038.481507     ) * t;
OMEGAA := sec_to_rad*	( ( ( ( +    0.0000003337   * t
								-    0.000000467  ) * t
								-    0.00772503   ) * t
								+    0.0512623    ) * t
								-    0.025754     ) * t + EPS0;
CHIA   := sec_to_rad*	( ( ( ( -    0.0000000560   * t
								+    0.000170663  ) * t
								-    0.00121197   ) * t
								-    2.3814292    ) * t
								+   10.556403     ) * t;
SA := SIN(EPS0);
CA := COS( EPS0 );
SB := SIN( -PSIA );
CB := COS( -PSIA );
SC := SIN( -OMEGAA );
CC := COS( -OMEGAA );
SD := SIN( CHIA );
CD := COS( CHIA );

// COMPUTE ELEMENTS OF PRECESSION ROTATION MATRIX
// EQUIVALENT TO R3(CHI_A)R1(-OMEGA_A)R3(-PSI_A)R1(EPSILON_0)
rot[0,0] :=  CD * CB - SB * SD * CC; 	rot[0,1] := CD * SB * CA + SD * CC * CB * CA - SA * SD * SC; 	rot[0,2] := CD * SB * SA + SD * CC * CB * SA + CA * SD * SC;
rot[1,0] := -SD * CB - SB * CD * CC; 	rot[1,1] := -SD * SB * CA + CD * CC * CB * CA - SA * CD * SC; 	rot[1,2] := -SD * SB * SA + CD * CC * CB * SA + CA * CD * SC;
rot[2,0] := SB * SC;					rot[2,1] := -SC * CB * CA - SA * CC; 							rot[2,2] := -SC * CB * SA + CC * CA;

// calculation as: Explanatory Supplement to the Astronomical Almanac (2013) p.220 eq.6.30
//x2 := rot* x; //rotate to get output direction cosines
IF equinox2 = 2000.0 THEN
	// PERFORM ROTATION FROM J2000.0 TO EPOCH
	x2[0] := rot[0,0]*x[0] + rot[1,0]*x[1] + rot[2,0]*x[2];
	x2[1] := rot[0,1]*x[0] + rot[1,1]*x[1] + rot[2,1]*x[2];
	x2[2] := rot[0,2]*x[0] + rot[1,2]*x[1] + rot[2,2]*x[2];
ELSE
	//  PERFORM ROTATION FROM EPOCH TO J2000.0
	x2[0] := rot[0,0]*x[0] + rot[0,1]*x[1] + rot[0,2]*x[2];
	x2[1] := rot[1,0]*x[0] + rot[1,1]*x[1] + rot[1,2]*x[2];
	x2[2] := rot[2,0]*x[0] + rot[2,1]*x[1] + rot[2,2]*x[2];
END_IF


ra_rad := atan2(x2[1],x2[0]);
dec_rad := ASIN(x2[2]);

ra_precessed := ra_rad/d2r;
ra_precessed := MODABS(ra_precessed, 360.0);            //RA between 0 AND 360 degrees
dec_precessed := dec_rad/d2r;]]></ST>
    </Implementation>
    <LineIds Name="FB_PRECESS">
      <LineId Id="79" Count="0" />
      <LineId Id="226" Count="0" />
      <LineId Id="285" Count="0" />
      <LineId Id="232" Count="0" />
      <LineId Id="280" Count="4" />
      <LineId Id="233" Count="0" />
      <LineId Id="227" Count="0" />
      <LineId Id="80" Count="1" />
      <LineId Id="72" Count="1" />
      <LineId Id="12" Count="1" />
      <LineId Id="236" Count="1" />
      <LineId Id="235" Count="0" />
      <LineId Id="231" Count="0" />
      <LineId Id="230" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="204" Count="2" />
      <LineId Id="239" Count="0" />
      <LineId Id="242" Count="0" />
      <LineId Id="256" Count="0" />
      <LineId Id="244" Count="11" />
      <LineId Id="241" Count="0" />
      <LineId Id="258" Count="6" />
      <LineId Id="208" Count="0" />
      <LineId Id="269" Count="0" />
      <LineId Id="268" Count="0" />
      <LineId Id="270" Count="1" />
      <LineId Id="267" Count="0" />
      <LineId Id="265" Count="0" />
      <LineId Id="278" Count="1" />
      <LineId Id="272" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="273" Count="1" />
      <LineId Id="220" Count="0" />
      <LineId Id="275" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="276" Count="1" />
      <LineId Id="221" Count="0" />
      <LineId Id="213" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="35" Count="4" />
      <LineId Id="41" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>