<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_HOR2EQ" Id="{a0256175-d56a-4186-a182-fc276b1c60fe}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_HOR2EQ
VAR_INPUT
	hor_coord:	S_SkyCoordHorizontal;	// INPUT COORD ALT/AZ in degrees
	jd: 		LREAL;					// Julian Date
	location:	S_EarthLocation;		// LOCATION of observer
	ws:			BOOL := FALSE; 			// Set this to TRUE get the azimuth measured westward FROM south (NOT East of North).
	precess:	BOOL := TRUE; 			// Set this to 1 to force precession [default], 0 for no precession correction
	nutate:		BOOL := TRUE; 			// Set this to 1 to force nutation [default], 0 for no nutation correction.
	aberration:	BOOL := TRUE; 			// Set this to 1 to force aberration correction [default], 0 for no correction.
	refract:	BOOL := TRUE; 			// Set to 1 to force refraction correction[default] (for actual sky coord), 0 for no correction (for theoretical coord).
END_VAR
VAR_OUTPUT
	eq_coord:	S_SkyCoordEquatorial :=  (ra:= 0.0, dec:= 0.0);;	// OUTPUT COORD RA/DEC (J2000) in degrees (FK5)
	ha:			LREAL;					// hour angle (in degrees) (optional)
END_VAR
VAR
	lmst,
	J_now: LREAL;
	co_nutate: 		FB_CO_NUTATE;
	co_aberration: 	FB_CO_ABERRATION;
	co_refract:		FB_CO_REFRACT;
	fbprecess:		FB_PRECESS;
	altaz2hadec:	FB_ALTAZ2HADEC;
	eq_coord_diff_nut:	S_SkyCoordEquatorial	:=  (ra:= 0.0, dec:= 0.0); // difference due to nutation
	eq_coord_diff_abr:	S_SkyCoordEquatorial	:=  (ra:= 0.0, dec:= 0.0); // difference due to aberration
	time_coord:			S_SkyCoordEquatorial 	:=  (ra:= 45.0, dec:= 45.0); // used for LOCAL APPARENT SIDEREAL TIME (180deg,0dec)
	last: LREAL;
	eps: LREAL;
	d_psi: LREAL;
END_VAR
VAR CONSTANT
	// easier then functions
	d2r:	LREAL := PI/180.0;
	h2d:	LREAL := 15.0;
	jd2000: LREAL := 2451545.0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* https://github.com/wlandsman/IDLAstro/blob/master/pro/hor2eq.pro
; NAME:
;   HOR2EQ
;
; PURPOSE:
;    Converts local horizon coords (alt-az) of something to equatorial (ra-dec).
;
; EXPLANATION:
;     This is a nice code to calculate equatorial (ra,dec) coordinates from
;     horizon (alt,az) coords.    It is typically accurate to about 1 arcsecond
;     or better (I have checked the output against the publicly available XEPHEM
;     software). It performs precession, nutation, aberration, and refraction
;     corrections.  The perhaps best thing about it is that it can take arrays
;     as inputs, in all variables and keywords EXCEPT Lat, lon, and Altitude
;    (the code assumes these aren't changing), and uses vector arithmetic in
;     every calculation except when calculating the precession matrices.
;
; DEPENDENCIES:
;       NUTATE, PRECESS, SUNPOS,
;       CO_NUTATE, CO_ABERRATION, CO_REFRACT, HADEC2ALTAZ
;CORRECTIONS I DO NOT MAKE:
;   *  Deflection of Light by the sun due to GR. (typically milliarcseconds,
;        can be arcseconds within one degree of the sun)
;   *  The Effect of Annual Parallax (typically < 1 arcsecond)
;   *  and more (see below)
;
; TO DO
;    * Better Refraction Correction.  Need to put in wavelength dependence,
;       and integrate through the atmosphere.
;    * Topocentric Parallax Correction (will take into account elevation of
;          the observatory)
;    * Proper Motion (but this will require crazy lookup tables or something).
;    * Difference between UTC and UT1 in determining LAST -- is this important?
;    * Effect of Annual Parallax (is this the same as topocentric Parallax?)
;    * Polar Motion
;    * Better connection to Julian Date Calculator.
;
; AUTHOR:
;   Chris O'Dell
;   Assistant Professor of Atmospheric Science
;   Colorado State University
;   Email: odell@atmos.colostate.edu
; REVISION HISTORY:
;     Made all integers type LONG  W. Landsman   September 2007
;     Fixed for case of scalar Julian date but vector positions W L June 2009
;-*)

IF WS THEN 
	hor_coord.az := hor_coord.az - 180.0;
END_IF

(********************************************************************************************
 Make Correction for ATMOSPHERIC REFRACTION
 (use this for visible and radio wavelengths; author is unsure about other wavelengths.
  See the comments in CO_REFRACT for more details.) *)
IF refract THEN 
	co_refract(	old_alt := hor_coord.alt, 
				altitude := location.altitude, 
				to_observed := TRUE, 
				new_alt => hor_coord.alt);
END_IF


//******************************************************************************
//Calculate LOCAL APPARENT SIDEREAL TIME
// Nutation for d_psi and eps
co_nutate(jd := jd, 
				eq_coord_in := time_coord, 
				eps=>eps, 
				d_psi=>d_psi);
// first get local mean sidereal time (lmst)
// get LST (in hours) - note:this is indep of tzone since giving jd
// Calculate LOCAL MEAN SIDEREAL TIME 
lmst := jd2lst(JD:= jd, loc:= location);
// calculate local APPARENT sidereal time
last := lmst + d_psi *COS(eps)/3600.0 ; //add correction in degrees


//****************************************************************************
// Now do the spherical trig to get APPARENT Hour Angle [degrees], and
// declination [degrees].
altaz2hadec( hor_coord:= hor_coord, lat := location.latitude, ha => ha, dec => eq_coord.dec);

// Find Right Ascension (in degrees, from 0 to 360.)
eq_coord.ra := MODABS(last - ha + 360.0, 360.0);

//*****************************************************************************
// calculate NUTATION and ABERRATION Corrections to Ra-Dec
IF nutate THEN
	co_nutate(	jd := jd, 
				eq_coord_in := eq_coord, 
				eq_coord_diff => eq_coord_diff_nut, 
				eps=>eps, 
				d_psi=>d_psi);
END_IF
IF ABERRATION THEN	
	co_aberration(	jd := jd, 
					eq_coord_in := eq_coord,
					eq_coord_diff => eq_coord_diff_abr, 
					eps := eps);
END_IF
eq_coord.ra := eq_coord.ra - (eq_coord_diff_abr.ra + eq_coord_diff_nut.ra);
eq_coord.dec := eq_coord.dec - (eq_coord_diff_abr.dec + eq_coord_diff_nut.dec);
 


(* ******************************************************************************
 PRECESS coordinates to current date
 (uses astro lib procedure PRECESS.pro)
*)
J_now := (jd - jd2000)/365.25 + 2000.0; // compute current equinox
IF precess THEN
	fbprecess( eq_coord_in := eq_coord, 
	equinox1 := J_now, equinox2 := 2000.0, 
	eq_coord_precessed => eq_coord);
END_IF
]]></ST>
    </Implementation>
    <LineIds Name="FB_HOR2EQ">
      <LineId Id="454" Count="0" />
      <LineId Id="587" Count="0" />
      <LineId Id="455" Count="14" />
      <LineId Id="519" Count="2" />
      <LineId Id="533" Count="16" />
      <LineId Id="579" Count="7" />
      <LineId Id="452" Count="1" />
      <LineId Id="261" Count="1" />
      <LineId Id="47" Count="0" />
      <LineId Id="263" Count="0" />
      <LineId Id="252" Count="8" />
      <LineId Id="129" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="57" Count="2" />
      <LineId Id="307" Count="0" />
      <LineId Id="303" Count="1" />
      <LineId Id="306" Count="0" />
      <LineId Id="296" Count="0" />
      <LineId Id="60" Count="1" />
      <LineId Id="383" Count="0" />
      <LineId Id="265" Count="2" />
      <LineId Id="65" Count="0" />
      <LineId Id="68" Count="8" />
      <LineId Id="436" Count="15" />
      <LineId Id="80" Count="0" />
      <LineId Id="384" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="337" Count="9" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>