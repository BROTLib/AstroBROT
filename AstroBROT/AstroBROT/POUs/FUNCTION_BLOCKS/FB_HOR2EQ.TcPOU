<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_HOR2EQ" Id="{a0256175-d56a-4186-a182-fc276b1c60fe}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_HOR2EQ
VAR_INPUT
	alt				: LREAL;			//altitude (in degrees)  
	az				: LREAL;			//azimuth angle (in degrees, measured EAST from NORTH, but see keyword WS below.) [scalar or vector]
	jd				: LREAL;			// Julian Date
	lat				: LREAL;			// observers latitude (degrees)
	lon				: LREAL;			// observers longitude (degrees)
	ws				: BOOL := FALSE; 	// Set this to TRUE get the azimuth measured westward FROM south (NOT East of North).
	precess			: BOOL := TRUE; 	// Set this to 1 to force precession [default], 0 for no precession correction
	nutate			: BOOL := TRUE; 	// Set this to 1 to force nutation [default], 0 for no nutation correction.
	aberration		: BOOL := TRUE; 	// Set this to 1 to force aberration correction [default], 0 for no correction.
	refract			: BOOL := TRUE; 	// Set to 1 to force refraction correction[default] (for actual sky coord), 0 for no correction (for theoretical coord).
	altitude		: LREAL := 0.0;
	refract_to_observed : BOOL := TRUE;
END_VAR
VAR_OUTPUT
	ra				: LREAL;			// Right Ascension of object  (J2000) in degrees (FK5)
	dec				: LREAL;			// Declination of object (J2000) in degrees (FK5)
	ha				: LREAL;			// hour angle (in degrees) (optional)
END_VAR
VAR
	lmst			: LREAL;
	J_now			: LREAL;
	co_nutate		: FB_CO_NUTATE;
	co_aberration	: FB_CO_ABERRATION;
	co_refract		: FB_CO_REFRACT;
	fbprecess		: FB_PRECESS;
	altaz2hadec		: FB_ALTAZ2HADEC;
	IAU2000B		: FB_IAU2000B;
	last			: LREAL;
	eps,eps0,d_eps	: LREAL;
	d_psi			: LREAL;
	d_ra_abr		: LREAL;		// Difference in RA due to aberration
	d_dec_abr		: LREAL;		// Difference in Dec due to aberration
	d_ra_nut		: LREAL;		// Difference in RA due to nutation
	d_dec_nut		: LREAL;		// Difference in Dec due to nutation
	T				: LREAL;
	d_dec_ref		: LREAL;		// time reference
	
END_VAR
VAR CONSTANT
	// easier then functions
	d2r				: LREAL := PI/180.0;
	h2d				: LREAL := 15.0;
	jd2000			: LREAL := 2451545.0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* https://github.com/wlandsman/IDLAstro/blob/master/pro/hor2eq.pro
; NAME:
;   HOR2EQ
;
; PURPOSE:
;    Converts local horizon coords (alt-az) of something to equatorial (ra-dec).
;
; EXPLANATION:
;     This is a nice code to calculate equatorial (ra,dec) coordinates from
;     horizon (alt,az) coords.    It is typically accurate to about 1 arcsecond
;     or better (I have checked the output against the publicly available XEPHEM
;     software). It performs precession, nutation, aberration, and refraction
;     corrections.  The perhaps best thing about it is that it can take arrays
;     as inputs, in all variables and keywords EXCEPT Lat, lon, and Altitude
;    (the code assumes these aren't changing), and uses vector arithmetic in
;     every calculation except when calculating the precession matrices.
;
; DEPENDENCIES:
;       NUTATE, PRECESS, SUNPOS,
;       CO_NUTATE, CO_ABERRATION, CO_REFRACT, HADEC2ALTAZ
;CORRECTIONS I DO NOT MAKE:
;   *  Deflection of Light by the sun due to GR. (typically milliarcseconds,
;        can be arcseconds within one degree of the sun)
;   *  The Effect of Annual Parallax (typically < 1 arcsecond)
;   *  and more (see below)
;
; TO DO
;    * Better Refraction Correction.  Need to put in wavelength dependence,
;       and integrate through the atmosphere.
;    * Topocentric Parallax Correction (will take into account elevation of
;          the observatory)
;    * Proper Motion (but this will require crazy lookup tables or something).
;    * Difference between UTC and UT1 in determining LAST -- is this important?
;    * Effect of Annual Parallax (is this the same as topocentric Parallax?)
;    * Polar Motion
;    * Better connection to Julian Date Calculator.
;
; AUTHOR:
;   Chris O'Dell
;   Assistant Professor of Atmospheric Science
;   Colorado State University
;   Email: odell@atmos.colostate.edu
; REVISION HISTORY:
;     Made all integers type LONG  W. Landsman   September 2007
;     Fixed for case of scalar Julian date but vector positions W L June 2009
;-*)

IF WS THEN 
	az := az - 180.0;
END_IF

(********************************************************************************************
 Make Correction for ATMOSPHERIC REFRACTION
 (use this for visible and radio wavelengths; author is unsure about other wavelengths.
  See the comments in CO_REFRACT for more details.) *)
IF refract THEN 
	co_refract(	
		old_alt := alt, 
		altitude := altitude, 
		to_observed := refract_to_observed, 
		new_alt => alt);
END_IF


//******************************************************************************
//Calculate LOCAL APPARENT SIDEREAL TIME
// Nutation for d_psi and eps
// must calculate obliquity OF ecliptic
T := (jd -jd2000)/36525.0; 
IAU2000B(jd := jd,
		d_psi => d_psi, 
		d_eps => d_eps); 

eps0 := 23.4392911*3600.0 - 46.8150*T - 0.00059*EXPT(T, 2) + 0.001813*EXPT(T, 3);
eps := (eps0 + d_eps)/3600.0*d2r; // TRUE obliquity OF the ecliptic in radians
// first get local mean sidereal time (lmst)
// get LST (in hours) - note:this is indep of tzone since giving jd
// Calculate LOCAL MEAN SIDEREAL TIME 
lmst := jd2lst(JD:=jd, lon:=lon);
// calculate local APPARENT sidereal time
last := lmst + d_psi *COS(eps)/3600.0 ; //add correction in degrees


//****************************************************************************
// Now do the spherical trig to get APPARENT Hour Angle [degrees], and
// declination [degrees].
altaz2hadec(alt := alt, az := az, lat := lat, ha => ha, dec => dec);

// Find Right Ascension (in degrees, from 0 to 360.)
ra := MODABS(last - ha + 360.0, 360.0);

//*****************************************************************************
// calculate NUTATION and ABERRATION Corrections to Ra-Dec
IF nutate THEN
	co_nutate(	
		jd := jd, 
		ra := ra, 
		dec := dec, 
		d_ra => d_ra_nut, 
		d_dec => d_dec_nut, 
		eps=>eps, 
		d_psi=>d_psi);
END_IF
IF ABERRATION THEN	
	co_aberration(	
		jd := jd, 
		ra := ra, 
		dec := dec,
		eps := eps,
		d_ra => d_ra_abr, 
		d_dec => d_dec_abr);
END_IF
ra := ra - (d_ra_abr + d_ra_nut);
dec := dec - (d_dec_abr + d_dec_nut);
 


(* ******************************************************************************
 PRECESS coordinates to current date
 (uses astro lib procedure PRECESS.pro)
*)
J_now := (jd - jd2000)/365.25 + 2000.0; // compute current equinox
IF precess THEN
	fbprecess( 
		ra := ra, 
		dec := dec, 
		equinox1 := J_now, 
		equinox2 := 2000.0, 
		ra_precessed => ra, 
		dec_precessed => dec);
END_IF
]]></ST>
    </Implementation>
    <LineIds Name="FB_HOR2EQ">
      <LineId Id="454" Count="0" />
      <LineId Id="587" Count="0" />
      <LineId Id="455" Count="14" />
      <LineId Id="519" Count="2" />
      <LineId Id="533" Count="16" />
      <LineId Id="579" Count="7" />
      <LineId Id="452" Count="1" />
      <LineId Id="261" Count="1" />
      <LineId Id="47" Count="0" />
      <LineId Id="263" Count="0" />
      <LineId Id="252" Count="5" />
      <LineId Id="632" Count="0" />
      <LineId Id="258" Count="2" />
      <LineId Id="129" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="57" Count="2" />
      <LineId Id="307" Count="0" />
      <LineId Id="703" Count="0" />
      <LineId Id="709" Count="0" />
      <LineId Id="704" Count="4" />
      <LineId Id="296" Count="0" />
      <LineId Id="60" Count="1" />
      <LineId Id="383" Count="0" />
      <LineId Id="265" Count="2" />
      <LineId Id="65" Count="0" />
      <LineId Id="68" Count="8" />
      <LineId Id="436" Count="3" />
      <LineId Id="646" Count="0" />
      <LineId Id="440" Count="0" />
      <LineId Id="647" Count="2" />
      <LineId Id="442" Count="4" />
      <LineId Id="651" Count="0" />
      <LineId Id="447" Count="0" />
      <LineId Id="652" Count="0" />
      <LineId Id="655" Count="0" />
      <LineId Id="653" Count="1" />
      <LineId Id="450" Count="1" />
      <LineId Id="80" Count="0" />
      <LineId Id="384" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="337" Count="6" />
      <LineId Id="656" Count="1" />
      <LineId Id="344" Count="0" />
      <LineId Id="658" Count="0" />
      <LineId Id="345" Count="0" />
      <LineId Id="659" Count="0" />
      <LineId Id="346" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>