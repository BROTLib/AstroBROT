<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_HADEC2RADEC" Id="{8f7a7748-90e3-4e16-8cf9-41e658f19889}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_HADEC2RADEC
VAR_INPUT
	jd				: LREAL;		// Julian date
	ha				: LREAL;		// Hour angle (APPARENT) in degrees
	dec				: LREAL;		// Declination (APPARENT) in degrees
	lon				: LREAL;		// Longitude in degrees
END_VAR
VAR_OUTPUT
	ra_out				: LREAL;		// RA in degrees (ICRS)
	dec_out			: LREAL;			// DEC in degrees (ICRS)
END_VAR
VAR
	lmst			: LREAL;
	last			: LREAL;
	d_psi			: LREAL;
	d_eps			: LREAL;
	eps				: LREAL;
	eps0			: LREAL;
	T				: LREAL;
	J_now			: LREAL;
	nutate			: FB_IAU2000B;
	fbprecess		: FB_PRECESS;
	co_nutate		: FB_CO_NUTATE;
	co_aberration	: FB_CO_ABERRATION;
	d_ra_abr		: LREAL;		// Difference in RA due to aberration
	d_dec_abr		: LREAL;		// Difference in Dec due to aberration
	d_ra_nut		: LREAL;		// Difference in RA due to nutation
	d_dec_nut		: LREAL;		// Difference in Dec due to nutation
	alt,az			: LREAL;
END_VAR
VAR CONSTANT
	d2r				: LREAL := PI/180.0;
	jd2000			: LREAL := 2451545.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// This code calculates the Right Ascension and Declination (ICRS) from the Hour Angle and Declination (APPARENT)
// This includes P03 precession and IAU2000B nutation as well as aberration (IDL libary)
// adapted from: https://github.com/wlandsman/IDLAstro/blob/master/pro/hor2eq.pro (IDL libary)
// this version is written by Lukas Melzig


// Julian centuries FROM J2000 OF jd.
T := (jd -jd2000)/36525.0; 

// must calculate obliquity OF ecliptic
nutate(jd := jd,
		d_psi => d_psi, 
		d_eps => d_eps); 

eps0 := 23.4392911*3600.0 - 46.8150*T - 0.00059*EXPT(T, 2) + 0.001813*EXPT(T, 3);
eps := (eps0 + d_eps)/3600.0*d2r; // TRUE obliquity OF the ecliptic in radians

// Calculate LOCAL MEAN SIDEREAL TIME 
lmst := jd2lst(JD:=jd, lon:=lon);
// calculate local APPARENT sidereal time
last := lmst + d_psi *COS(eps)/3600.0 ; //add correction in degrees

// Find right ascension (in DEGREES)
ra_out := last - ha;
dec_out := dec;
J_now := (jd - jd2000)/365.25 + 2000.0; // compute current equinox
//*****************************************************************************
// calculate NUTATION and ABERRATION Corrections to Ra-Dec
co_nutate(	
		jd := jd, 
		ra := ra_out, 
		dec := dec_out, 
		d_ra => d_ra_nut, 
		d_dec => d_dec_nut);

co_aberration(	
		jd := jd, 
		ra := ra_out, 
		dec := dec_out,
		eps := eps,
		d_ra => d_ra_abr, 
		d_dec => d_dec_abr);
//apply
ra_out := ra_out - (d_ra_abr + d_ra_nut);
dec_out := dec_out - (d_dec_abr + d_dec_nut);
//*****************************************************************************
//precess back
fbprecess( 
	ra := ra_out, 
	dec := dec_out, 
	equinox1 := J_now, 
	equinox2 := 2000.0, 
	ra_precessed => ra_out, 
	dec_precessed => dec_out);

]]></ST>
    </Implementation>
    <LineIds Name="FB_HADEC2RADEC">
      <LineId Id="86" Count="0" />
      <LineId Id="315" Count="2" />
      <LineId Id="145" Count="0" />
      <LineId Id="275" Count="0" />
      <LineId Id="256" Count="18" />
      <LineId Id="159" Count="2" />
      <LineId Id="163" Count="4" />
      <LineId Id="170" Count="0" />
      <LineId Id="172" Count="9" />
      <LineId Id="158" Count="0" />
      <LineId Id="226" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="147" Count="6" />
      <LineId Id="230" Count="0" />
      <LineId Id="144" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>